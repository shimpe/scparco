TITLE:: ParserFactory
summary:: a factory for commonly used parsers, made from smaller library provided parsers
categories:: Parser Tools
related:: Classes/Parser

DESCRIPTION::
ParserFactory is a util that can create some parsers and families of parsers for common use cases. These parsers are built as combinations of more primitive library provided parsers. In some cases (e.g. html tag parser), you may want to build your own parser to make it easier to extract all information. ParserFactory only implements classmethods, meaning you can call them using the syntax ParserFactory.makeIntegerParser (i.e. there's no need to create a ParserFactory instance first).



CLASSMETHODS::

METHOD:: new
creates a new ParserFactory

returns:: a new ParserFactory
METHOD:: makeBetween
creates a parser factory that can extract something between two other parsers.
	So, given a string and 3 parsers [LeftParser, ContentParser, RightParser], it will return only what is found by the ContentParser. Think of a value between brackets, where you are only interested in what is between the brackets (see example code).

	The result of a call to makeBetween is not a complete parser yet. It is a function that expects the ContentParser as argument. This allows to prepare a "between parser", where the LeftParser and RightParser are specified when makeBetween is called, and the ContentParser is specified at the moment of finalizing the parser. To finalize the parser, call the function return by makeBetween with a ContentParser as argument.

ARGUMENT:: leftParser
a Parser that matches whatever should be left of the content you are interested in

ARGUMENT:: rightParser
a Parser that matches whatever should be right of the content you are interested in

returns:: a function that, when called with a ContentParser as argument, will produce a new parser that matches first LeftParser, then ContentParser, then RightParser and keeps only the result of the ContentParser.
METHOD:: makeIntegerParser
class to parse an integer number and convert it from string to Integer

returns:: Parser that can parse a (signed) integer number
METHOD:: makeFloatParser
class to parse a float number and convert it from string to Float

returns:: Parser that can parse a (signed) float number
METHOD:: makeEmailParser
class to parse an email address

returns:: Parser that can parse an email address (some extremely exotic uses cases are excluded)
METHOD:: makeUrlParser
class to parse a URL

returns:: Parser that can parse a URL
METHOD:: makeLetters
class to parse one or more letters

returns:: Parser that can parse one or more letters
METHOD:: makeDigits
class to parse one or more digits

returns:: Parser that can parse one or more digits
METHOD:: makeAlphanumeric
class to parse an alphanumeric value

returns:: Parser that can parse an alphanumeric value
METHOD:: makeIpAddressParser
class to parse an IPv4/IPv6 address

returns:: Parser that can parse an IPv4/IPv6 address
METHOD:: makeYYYYMMddParser
class to parse an YYY-MM-dd date (separator - . or /)

returns:: Parser that can parse an an YYY-MM-dd date 
METHOD:: makeddMMYYYYParser
class to parse an dd-MM-YYYY parser (separators - . or /) 

returns:: Parser that can parse an dd-MM-YYYY date
METHOD:: makeHtmlTagParser
class to match an html tag (with attributes). The tag with attributes is matched as a whole. No subparsers are present to find the individual attributes.

returns:: Parser that can parse an html tag with attributes
METHOD:: makeWs
class to match zero or more whitespace characters

returns:: Parser that parses multiple whitespace characters [\t\r\n\f]
METHOD:: makeWsOne
class to match one or more whitespace characters

returns:: Parser that parses multiple whitespace characters [\t\r\n\f]
METHOD:: makeIdentifierParser
class to match an identifier as used in many programming languages

returns:: Parser that parses an identifier
METHOD:: makeSepBy
generates a function that expects a ValueParser and produces a parser that matches zero or more separated values (see example code).

ARGUMENT:: separatorParser
a Parser that matches whatever separates the values we are interested in

returns:: a function that expects a ValueParser as argument and produces a Parser that matches zero or more separated values
METHOD:: makeSepByOne
generates a function that expects a ValueParser and produces a parser that matches one or more separated values (see example code).

ARGUMENT:: separatorParser
a Parser that matches whatever separates the values we are interested in

returns:: a function that expects a ValueParser as argument and produces a Parser that matches one or more separated values
METHOD:: makeBinaryUIntParser
generates a parser that extracts an n-bit unsigned integer from a binary message(contained in an Int8Array).

ARGUMENT:: no_of_bits
an integer in the interval [1,32]

returns:: a Parser that extracts an n-bit unsigned integer from a binary message (contained in an Int8Array)
METHOD:: makeBinaryIntParser
generates a parser that extracts an n-bit signed integer from a binary message(contained in an Int8Array).

ARGUMENT:: no_of_bits
an integer in the interval [1,32]

returns:: a Parser that extracts an n-bit signed integer from a binary message (contained in an Int8Array)
METHOD:: makeBinaryRawStringParser
generates a parser that matches a literal string (e.g. a magic marker) in a binary message(contained in an Int8Array).

ARGUMENT:: s
string to match

returns:: a Parser that extracts matches a literal string in a binary message (contained in an Int8Array)
METHOD:: makeBinaryLiteralInt8ArrayParser
generates a parser that matches a literal Int8Array (e.g. a magic marker) in a binary message(contained in an Int8Array).

ARGUMENT:: a
array to match

returns:: a Parser that extracts matches a literal Int8Array in a binary message (contained in an Int8Array)
METHOD:: forwardRef
forwardRef is used in recursive parsers, since without forward reference it's impossible to use parsers that have not been defined yet leading to a chicken-and-egg problem (in a recursive parser, parser A refers to parser B, but parser B refers to parser A again).

ARGUMENT:: parserThunk
a Thunk (= unevaluated function) that, when evaluated, will produce a parser. The Thunk can contain parsers that have not yet been defined in the code (see example code).

returns:: a parser wrapper that will evaluate to an actual parser only when it is being used during parsing. At that moment in time, all parsers it refers to must have been instantiated and initialized. This allows for making parser A depend on parser B, where parser B depends on parser A (circular dependencies, which often occur when defining recursive parsers).

INSTANCEMETHODS::

METHOD:: init
initializes the parser factory (but in reality it does nothing :))

returns:: ParserFactory

EXAMPLES::

code::
	(
	var intp = ParserFactory.makeIntegerParser;
	var result = intp.run("3.14"); // expected result: 3
	result.result.postcs;
	)
	(
	var floatp = ParserFactory.makeFloatParser;
	var result = floatp.run("3.14"); // expected result: 3.14
	result.result.postcs;
	)
	(
	var emailp = ParserFactory.makeEmailParser;
	var result = emailp.run("fubar.smalltalk@brainiac.co.uk");
	result.result.postcs; // result: "fubar.smalltalk@brainiac.co.uk"
	)
	(
	var urlp = ParserFactory.makeUrlParser;
	var result = urlp.run("https://www.google.com/givemebreak?parameter=value&&parameter2=56");
	result.result.postcs; // result: "https://www.google.com/givemebreak?parameter=value&&parameter2=56"
	)
	(
	var ip = ParserFactory.makeIpAddressParser;
	var result = ip.run("127.0.0.1");
	var result2 = ip.run("2001:0db8:85a3:0000:0000:8a2e:0370:7334");
	result.result.postcs;
	result2.result.postcs;
	)
	(
	var ymd = ParserFactory.makeYYYYMMddParser;
	var result = ymd.run("2023-06-02");
	result.result.postcs;
	)
	(
	var dmy = ParserFactory.makeddMMYYYYParser;
	var result = dmy.run("13/02/1975");
	result.result.postcs;
	)
	(
	var tag = ParserFactory.makeHtmlTagParser;
	var result = tag.run("<element value=\"56\">");
	result.result.postcs;
	)
	(
	var betweenBrackets = ParserFactory.makeBetween(StrParser("("), StrParser(")"));
	var lettersBetweenBrackets = betweenBrackets.(RegexParser("[A-Za-z]+"));
	var result = lettersBetweenBrackets.run("(foobarsnafu)"); // result should be "foobarsnafu"
	result.result.postcs;
	)
	(
	var commasep = ParserFactory.makeSepBy(StrParser(","));
	var csv_ints = commasep.(ParserFactory.makeIntegerParser);
	var result = csv_ints.run("45,78,-93,12"); // result should be [45, 78, -93, 12]
	result.result.postcs;
    )
    (
	// both separators and values can be more complex parsers as well
	var commacolonsep = ParserFactory.makeSepBy(Choice([StrParser(","), StrParser(";")]));
	var csv_numbers = commacolonsep.(ParserFactory.makeFloatParser);
	var text = "45,-89.23;56.7,67;90.09";
	var result = csv_numbers.run(text); // result should be [ 45.0, -89.23, 56.7, 67.0, 90.09 ]
	result.result.postcs;
    )
	(
	var betweenSquareBrackets = ParserFactory.makeBetween(StrParser("["), StrParser("]"));
	var commaSeparated = ParserFactory.makeSepBy(StrParser(","));
	var digitparser = ParserFactory.makeIntegerParser;
	var nested_array_parser = ParserFactory.forwardRef(Thunk({
	// Thunk makes sure its inside is not evaluated yet
	// and forwardRef "disguises" (wraps) the Thunk as a Parser
	//
	// We need a forward ref here to get around a mutual dependency problem
	// namely: nested_array_element uses flat_arrayparser, but
	// flat_arrayparser uses nested_array_element... (= mutually recursive definition).
	Choice([
	    digitparser,
		arrayparser])
	}));
	var arrayparser = betweenSquareBrackets.(commaSeparated.(nested_array_parser));
	var result = arrayparser.run("[1,[2,3,[4,5],6],7,[8],9]"); // expected result: [ 1, [ 2, 3, [ 4, 5 ], 6 ], 7, [ 8 ], 9 ]
	result.result.postcs;
	)
	(
	var t = Int8Array[0xF7, 0x00, 0x00, 0x41, 0xF0];
	var pl = [ParserFactory.makeBinaryLiteralInt8ArrayParser(Int8Array[0xF7])].addAll(BinaryBit() ! 8);
	var p2 = [ParserFactory.makeBinaryLiteralInt8ArrayParser(Int8Array[0xF7])].add(ParserFactory.makeBinaryIntParser(8));
	var sysexParser = SequenceOf([
	ParserFactory.makeBinaryLiteralInt8ArrayParser(Int8Array[0xF7])].add(
	ParserFactory.makeBinaryIntParser(8).chain({
	    | result |
	    // using chain we can influence the next parser:
	    // if the first byte is 0x00, two more bytes will follow with the manufacturer id
	    // otherwise, the byte value itself is already the manufacturer's id
	    if (result == 0) {
	        // extract extended ID and tag it as \manufacturerId in the parse result
	        ParserFactory.makeBinaryUIntParser(16).map(MapFactory.tag(\manufacturerId));
	    } {
	        // current ID is already extracted; tag it as \manufacturerId in the parse result
	        SucceedParser(result).map(MapFactory.tag(\manufacturerId));
	    }
	}))).map({|result| result[1]});
	var result;
	result = SequenceOf(sysexParser).run(t);
	result.result.postcs;
	)

	
::
